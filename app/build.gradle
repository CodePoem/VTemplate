apply plugin: 'com.android.application'

def keystorePWD = ''
def keystoreAlias = ''
def keystoreAliasPWD = ''
// local.properties file in the root director
def keyFile = project.rootProject.file('local.properties')

Properties properties = new Properties()
// local.properties exists
if (keyFile.exists()) {
    properties.load(keyFile.newDataInputStream())
} else {
    keyFile = file("../no_exists_keystore.tmp")
}

// local.properties contains keystore.path
if (properties.containsKey("keystore.path")) {
    keyFile = file(properties.getProperty("keystore.path"))
    keystorePWD = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPWD = properties.getProperty("keystore.alias_password")
} else {
    keyFile = file("../no_exists_keystore.tmp")
}

def runningOnTravis = System.getenv("CI") == "true"
if (runningOnTravis) {
    keyFile = file("../mrd@vdreamers")
    keystorePWD = System.getenv("KEYSTORE_PWD")
    keystoreAlias = System.getenv("KEYSTORE_ALIAS")
    keystoreAliasPWD = System.getenv("KEYSTORE_ALIAS_PWD")
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.vdreamers.vtemplate"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 100105000
        versionName "1.5.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias keystoreAlias
            keyPassword keystoreAliasPWD
            storeFile keyFile
            storePassword keystorePWD
        }
    }
    buildTypes {
        debug {
            //是否优化zip
            zipAlignEnabled false
            // 移除无用的resource文件
            shrinkResources false
            //是否开启混淆
            minifyEnabled false
            buildConfigField "boolean","RUNNING_ON_TRAVIS", "${runningOnTravis}"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            //是否优化zip
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            //是否开启混淆
            minifyEnabled true
            buildConfigField "boolean","RUNNING_ON_TRAVIS", "${runningOnTravis}"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keyFile.exists()) {
                println("WITH -> buildTypes -> release: using jks key")
                signingConfig signingConfigs.release
            } else {
                println("WITH -> buildTypes -> release: using default key")
                signingConfig signingConfigs.debug
            }
        }
        verify {
            initWith release
            debuggable true
        }
    }

    flavorDimensions "openness"

    productFlavors {
        internal {
            dimension 'openness'
            applicationIdSuffix ".internal"
        }
        published {
            dimension 'openness'
            versionNameSuffix "-published"
        }
    }

    applicationVariants.all { variant ->
        def buildType = variant.buildType.name
        def flavor = variant.flavorName
        variant.outputs.all {
            outputFileName = "Template-v${variant.versionName}-${flavor}-${buildType}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // 集成测试依赖
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // 单元测试依赖
    testImplementation 'androidx.test.ext:junit:1.1.1'
    // 业务依赖
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}
